взяти запит
взяти json
взяти типи

Оновлення = типи.Оновлення
Повідомлення = типи.Повідомлення
Чат = типи.Чат

ОновленняСтворити_з_телеграм_обʼєкта = типи.ОновленняСтворити_з_телеграм_обʼєкта

модуль ОтриматиОновлення

  шлях = "getUpdates"

  структура Запит
    зсув = пусто
  кінець

  дія Запит.перетворити_на_телеграм_обʼєкт() словник
    (
      offset=я.зсув
    )
  кінець

  структура Відповідь
    добре
    опис
    оновлення
  кінець

  дія перетворити_на_відповідь(то)
      результати = то["result"] як список

      оновлення = []
      перебрати результати як результат
        оновлення.додати(ОновленняСтворити_з_телеграм_обʼєкта(результат))
      кінець

      вернути Відповідь(
          добре=то["ok"],
          опис=то["description"],
          оновлення=оновлення
      )
  кінець

  дати шлях
  дати Запит
  дати Відповідь
  дати перетворити_на_відповідь

кінець

модуль НадіслатиПовідомлення

  шлях = "sendMessage"

  структура Запит
    ідентифікатор_чату текст
    ідентифікатор_теми_повідомлення = пусто
    текст текст = пусто
    відключити_сповіщення = пусто
    захист_вмісту = пусто
    відповісти_на_повідомлення_ід = пусто
    дозволити_відправку_без_відповіді = пусто
    ;; розмітка_відповіді = пусто
  кінець

  дія Запит.перетворити_на_телеграм_обʼєкт() словник
    (
      chat_id=я.ідентифікатор_чату,
      message_thread_id=я.ідентифікатор_теми_повідомлення,
      text=я.текст,
      disable_notification=я.відключити_сповіщення,
      protect_content=я.захист_вмісту,
      reply_to_message_id=я.відповісти_на_повідомлення_ід,
      allow_sending_without_reply=я.дозволити_відправку_без_відповіді
      ;; reply_markup=я.розмітка_відповіді ? я.розмітка_відповіді.перетворити_на_телеграм_обʼєкт() : я.розмітка_відповіді
    )
  кінець

  дати шлях
  дати Запит

кінець

модуль Телеграм

  структура Бот
    урл текст
    токен текст

    останнє_оновлення = пусто
  кінець

  тривала дія Бот.отриматиПовідомлення(непотріб = ()) список
    якщо я.останнє_оновлення
      запит = ОтриматиОновлення.Запит(зсув=я.останнє_оновлення.ідентифікатор_оновлення + 1)
    кінець

    чекати дані = я.виконати(ОтриматиОновлення.шлях, запит)
    відповідь = ОтриматиОновлення.перетворити_на_відповідь(дані)

    якщо !відповідь.добре
      впасти """Помилка отримання оновлень: %(відповідь.опис)"""
    кінець

    оновлення = відповідь.оновлення як список

    перебрати оновлення як о
      я.останнє_оновлення = о
    кінець

    вернути оновлення.перетворити(
      дія(оновлення) 
        вернути оновлення.повідомлення
      кінець
    )

  кінець

  тривала дія Бот.надіслатиПовідомлення(чат, текст текст)
    це_чат = чат як Чат
    запит = НадіслатиПовідомлення.Запит(ідентифікатор_чату=це_чат.ідентифікатор, текст=текст)
    чекати я.виконати(НадіслатиПовідомлення.шлях, запит)
  кінець

  тривала дія Бот.виконати(шлях текст, параметри обʼєкт) обʼєкт
    урл = я.урл + "/bot" + я.токен + "/" + шлях
    
    тіло словник = ()
    якщо параметри
      тіло = параметри.перетворити_на_телеграм_обʼєкт() як словник
    кінець

    чекати відповідь = запит.надіслати(
      "POST",
      урл,
      ("content-type"="application/json"),
      json.stringify(тіло)
    )
    вернути json.parse(текст(відповідь.дані))
  кінець

  дати Бот

кінець

дати Телеграм
