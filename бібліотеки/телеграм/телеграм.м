взяти запит
взяти json
взяти .типи

Оновлення = типи.Оновлення
Повідомлення = типи.Повідомлення
Чат = типи.Чат
ДодатковаКлавітура = типи.ДодатковаКлавітура

ОновленняСтворити_з_телеграм_обʼєкта = типи.ОновленняСтворити_з_телеграм_обʼєкта

модуль ОтриматиОновлення

  шлях = "getUpdates"

  структура Запит
    зсув = пусто
  кінець

  дія Запит.перетворити_на_телеграм_обʼєкт() словник
    (
      offset=я.зсув
    )
  кінець

  структура Відповідь
    добре
    опис
    оновлення
  кінець

  дія перетворити_на_відповідь(то)
      результати = то["result"] як список

      оновлення = []
      перебрати результати як результат
        оновлення.додати(ОновленняСтворити_з_телеграм_обʼєкта(результат))
      кінець

      вернути Відповідь(
          добре=то["ok"],
          опис=то["description"],
          оновлення=оновлення
      )
  кінець

  дати шлях
  дати Запит
  дати Відповідь
  дати перетворити_на_відповідь

кінець

модуль НадіслатиПовідомлення

  шлях = "sendMessage"

  структура Запит
    ідентифікатор_чату
    текст текст = пусто
    ;; розмітка_відповіді = пусто
  кінець

  дія Запит.перетворити_на_телеграм_обʼєкт() словник
    (
      chat_id=я.ідентифікатор_чату,
      text=я.текст
      ;; reply_markup=я.розмітка_відповіді ? я.розмітка_відповіді.перетворити_на_телеграм_обʼєкт() : я.розмітка_відповіді
    )
  кінець

  дати шлях
  дати Запит

кінець

модуль НадіслатиФото

  шлях = "sendPhoto"

  структура Запит
    ідентифікатор_чату
    фото текст = пусто
    текст текст = пусто
    додаткова_клавіатура ДодатковаКлавітура = пусто
  кінець

  дія Запит.перетворити_на_телеграм_обʼєкт() словник
    (
      chat_id=я.ідентифікатор_чату,
      photo=я.фото,
      caption=я.текст,
      reply_markup=я.додаткова_клавіатура ? json.stringify(я.додаткова_клавіатура.перетворити_на_телеграм_обʼєкт()) : я.додаткова_клавіатура
    )
  кінець

  дати шлях
  дати Запит

кінець


модуль Телеграм

  структура Бот
    урл текст
    токен текст

    останнє_оновлення = пусто
  кінець

  тривала дія Бот.отриматиПовідомлення(непотріб = ()) список
    якщо я.останнє_оновлення
      запит = ОтриматиОновлення.Запит(зсув=я.останнє_оновлення.ідентифікатор_оновлення + 1)
    кінець

    чекати дані = я.виконати(ОтриматиОновлення.шлях, запит)
    відповідь = ОтриматиОновлення.перетворити_на_відповідь(дані)

    якщо !відповідь.добре
      впасти """Помилка отримання оновлень: %(відповідь.опис)"""
    кінець

    оновлення = відповідь.оновлення як список

    перебрати оновлення як о
      я.останнє_оновлення = о
    кінець

    вернути оновлення.перетворити(
      дія(оновлення) 
        вернути оновлення.повідомлення
      кінець
    )

  кінець

  тривала дія Бот.надіслатиПовідомлення(ідентифікатор_чату, текст текст)
    запит = НадіслатиПовідомлення.Запит(ідентифікатор_чату=ідентифікатор_чату, текст=текст)
    чекати я.виконати(НадіслатиПовідомлення.шлях, запит)
  кінець

  тривала дія Бот.надіслатиГруВгадайку(ідентифікатор_чату, фото текст, текст текст, кнопки список)
    додаткова_клавіатура = ДодатковаКлавітура(тексти_кнопок=кнопки, кількість_в_рядку=2)
    запит = НадіслатиФото.Запит(ідентифікатор_чату=ідентифікатор_чату, фото=фото, текст=текст, додаткова_клавіатура=додаткова_клавіатура)
    чекати я.виконати(НадіслатиФото.шлях, запит)
  кінець


  тривала дія Бот.виконати(шлях текст, параметри обʼєкт) обʼєкт
    урл = я.урл + "/bot" + я.токен + "/" + шлях
    
    тіло словник = ()
    якщо параметри
      тіло = параметри.перетворити_на_телеграм_обʼєкт() як словник
    кінець

    чекати відповідь = запит.надіслати(
      "POST",
      урл,
      ("content-type"="application/json"),
      json.stringify(тіло)
    )
    вернути json.parse(текст(відповідь.дані))
  кінець

  дати Бот

кінець

дати Телеграм
